apply plugin: 'com.android.library'
apply plugin: 'maven'
apply from: '../lib_common.gradle'

android {
    compileSdkVersion 24
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 102
        versionName "4.0"
        multiDexEnabled true
        generatedDensities = []

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    packagingOptions {
        exclude 'META-INF/maven/com.squareup/javawriter/pom.properties'
        exclude 'META-INF/maven/com.squareup/javawriter/pom.xml'

        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    group = "geotrac.android"
    version = determineVersion("1.1")
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // Test
    testCompile 'junit:junit:4.12'
    androidTestCompile "com.android.support.test:runner:$rootProject.ext.testRunnerVersion"
    androidTestCompile "com.android.support:support-annotations:24.0.0"
    // Handle support annotation library conflict with testing support libraries
    configurations.androidTestCompile.dependencies.each { androidTestCompileDependency ->
        androidTestCompileDependency.exclude group: 'com.android.support'
    }

    // Design
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibVersion"
    compile "com.android.support:design:$rootProject.ext.supportLibVersion"
    compile "com.android.support:percent:$rootProject.ext.supportLibVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.ext.supportLibVersion"
    compile "com.android.support:support-v13:$rootProject.ext.supportLibVersion"

    // Image
    compile 'com.makeramen:roundedimageview:2.2.1'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.commonsware.cwac:cam2:0.6.2'
    compile 'com.github.bumptech.glide:glide:3.7.0'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: getPublishUrl()) {
                authentication(userName: "${nexus_username}", password: "${nexus_password}")
            }
        }
    }
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
